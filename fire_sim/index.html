<!doctype html>
<html>
  <head>
    <meta charset="UTF-8">
    <link rel="icon" href="candle.png"></link>
    <title>Candle Simulator</title>
    <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/87/three.min.js"></script>
    <script type="text/javascript" src="GUI/dat.gui.min.js"></script>
    <style>
        body { margin: 0; }
    </style>
  </head>
  <body>
    <script id="Advect" type="x-shader/x-fragment">
        precision mediump float;
        uniform vec2 res;//The width and height of our screen
        uniform sampler2D velocityField;//Our input texture
        uniform sampler2D advectionField;
        uniform float dissipation;

        vec3 bilerp(sampler2D texture, vec2 p)
        {
            // add 0.5 for center of pixel
            float s = p.x - (floor(p.x - 0.5) + 0.5);
            float t = p.y - (floor(p.y - 0.5) + 0.5);

            vec4 uv;
            uv.xy = (floor(p - 0.5) + 0.5) / res.xy;
            uv.zw = (floor(p - 0.5) + 1.5) / res.xy;

            vec3 u00 = texture2D(texture, uv.xy).xyz;
            vec3 u10 = texture2D(texture, uv.zy).xyz;

            vec3 u0 = mix(u00, u10, s);

            vec3 u01 = texture2D(texture, uv.xw).xyz;
            vec3 u11 = texture2D(texture, uv.zw).xyz;

            vec3 u1 = mix(u01, u11, s);

            return mix(u0, u1, t);
        }

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            vec2 p = gl_FragCoord.xy - texture2D(velocityField, pixel).xy;
            gl_FragColor = vec4(dissipation * bilerp(advectionField, p), 1.0);
        }
    </script>
    <script id="ExternalVelocity" type="x-shader/x-fragment">
        uniform vec2 res;//The width and height of our screen
        uniform sampler2D bufferTexture;//Our input texture
        uniform vec3 smokeSource;//The x,y are the posiiton. The z is the power/density
        uniform vec2 sourceVelocity;
        uniform float radius;

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            float dist = distance(smokeSource.xy,gl_FragCoord.xy);
            vec2 smoke = smokeSource.z * max(radius-dist,0.0) * sourceVelocity / res.xy * 0.8;
            gl_FragColor = vec4(texture2D(bufferTexture, pixel).xy + smoke, 0.0, 1.0);
        }
    </script>
    <script id="ExternalTemperature" type="x-shader/x-fragment">
        uniform vec2 res;//The width and height of our screen
        uniform sampler2D bufferTexture;//Our input texture
        uniform vec3 smokeSource;//The x,y are the posiiton. The z is the power/density
        uniform float temp;
        uniform float radius;

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            float dist = distance(smokeSource.xy,gl_FragCoord.xy);
            float smoke = smokeSource.z * max(radius-dist,0.0) * temp;
            gl_FragColor = vec4(texture2D(bufferTexture, pixel).x + smoke, 0.0, 0.0, 1.0);
        }
    </script>
    <script id="ExternalDensity" type="x-shader/x-fragment">
        uniform vec2 res;//The width and height of our screen
        uniform sampler2D bufferTexture;//Our input texture
        uniform vec3 smokeSource;//The x,y are the posiiton. The z is the power/density
        uniform vec3 color;
        uniform float radius;

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            float dist = distance(smokeSource.xy,gl_FragCoord.xy);
            vec3 smoke = vec3(smokeSource.z * max(radius-dist,0.0)/ res.x, smokeSource.z * max(radius-dist,0.0)/ res.x, smokeSource.z * max(radius-dist,0.0)/ res.x) * color;
            gl_FragColor = vec4(texture2D(bufferTexture, pixel).xyz + smoke, 1.0);
        }
    </script>
    <script id="Draw" type="x-shader/x-fragment">
        precision mediump float;
        uniform vec2 res;
        uniform sampler2D bufferTexture;
        uniform vec3 bias;
        uniform vec3 scale;

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            gl_FragColor = vec4(bias + scale * texture2D(bufferTexture, pixel).xyz, 1.0);
        }
    </script>
    <script id="Jacobi" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D x;
        uniform sampler2D b;
        uniform float alpha;
        uniform float beta;

        void main() {
            vec2 istep = vec2(1.0 / res.x, 0.0);
            vec2 jstep = vec2(0.0, 1.0 / res.y);
            vec2 ij = gl_FragCoord.xy / res.xy;

            gl_FragColor = vec4( (texture2D(x, ij - istep).x + texture2D(x, ij + istep).x + texture2D(x, ij + jstep).x + texture2D(x, ij - jstep).x + alpha * texture2D(b, ij).x) / beta, 0.0, 0.0, 1.0);


        }
    </script>
    <script id="Divergence" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D u;
        uniform float dx;
        uniform float dy;

        void main() {
            vec2 istep = vec2(1.0 / res.x, 0.0);
            vec2 jstep = vec2(0.0, 1.0 / res.y);
            vec2 ij = gl_FragCoord.xy / res.xy;

            float uterm = (texture2D(u, ij + istep).x - texture2D(u, ij - istep).x) / (2.0 * dx);
            float vterm = (texture2D(u, ij + jstep).y - texture2D(u, ij - jstep).y) / (2.0 * dy);

            gl_FragColor = vec4(uterm + vterm, 0.0, 0.0, 1.0);

        }
    </script>
    <script id="SubtractGradient" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D w;
        uniform sampler2D p;
        uniform float dx;
        uniform float dy;

        void main() {
            vec2 istep = vec2(1.0 / res.x, 0.0);
            vec2 jstep = vec2(0.0, 1.0 / res.y);
            vec2 ij = gl_FragCoord.xy / res.xy;

            vec2 gradient = vec2((texture2D(p, ij + istep).x - texture2D(p, ij - istep).x) / (2.0 * dx), (texture2D(p, ij + jstep).x - texture2D(p, ij - jstep).x) / (2.0 * dy));

            gl_FragColor = vec4(texture2D(w, ij).xy - gradient, 0.0, 1.0);

        }
    </script>
    <script id="Buoyancy" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D velocityField;
        uniform sampler2D temperatureField;
        uniform sampler2D densityField;
        uniform float ambientTemperature;
        uniform float sigma;
        uniform float kappa;

        void main() {
            vec2 pixel = gl_FragCoord.xy / res.xy;
            float t = texture2D(temperatureField, pixel).x;
            vec4 v = texture2D(velocityField, pixel);

            gl_FragColor = v;

            //if (t > ambientTemperature) {
                float d = texture2D(densityField, pixel).x;
                gl_FragColor += vec4(0.0, sigma * (t - ambientTemperature) - d * kappa, 0.0, 1.0);
            //}


        }
    </script>
    <script id="Curl" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D velocityField;
        uniform float dx;
        uniform float dy;

        void main() {
            vec2 istep = vec2(1.0 / res.x, 0.0);
            vec2 jstep = vec2(0.0, 1.0 / res.y);
            vec2 ij = gl_FragCoord.xy / res.xy;

            float dvdx = (texture2D(velocityField, ij + istep).y - texture2D(velocityField, ij - istep).y) / (2.0 * dx);
            float dudy = (texture2D(velocityField, ij + jstep).x - texture2D(velocityField, ij - jstep).x) / (2.0 * dy);

            gl_FragColor = vec4(dvdx - dudy, 0.0, 0.0, 1.0);
        }
    </script>
    <script id="VorticityConf" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D velocityField;
        uniform sampler2D curlField;
        uniform float eps;
        uniform float dt;
        uniform float dx;
        uniform float dy;
        uniform float weight;
        uniform float boundary;

        void main() {
            vec2 istep = vec2(1.0 / res.x, 0.0);
            vec2 jstep = vec2(0.0, 1.0 / res.y);
            vec2 ij = gl_FragCoord.xy / res.xy;

            float etaX = (abs(texture2D(curlField, ij + istep).x) - abs(texture2D(curlField, ij - istep).x)) / (2.0 * dx);
            float etaY = (abs(texture2D(curlField, ij + jstep).x) - abs(texture2D(curlField, ij - jstep).x)) / (2.0 * dy);
            vec2 force = vec2(0.0);
            if (abs(etaX) > eps && abs(etaY) > eps) {

                vec2 psi = vec2(etaX / abs(etaX), etaY / abs(etaY));
                vec2 vorticity = texture2D(curlField, ij).xy;
                vec2 cross = vec2(psi.y * vorticity.x, psi.x * vorticity.y * -1.0);
                force = cross * dt;
            }
            vec2 velocity = texture2D(velocityField, ij).xy;
            gl_FragColor = vec4(velocity + weight * force, 0.0, 1.0);


            if (boundary > 0.1) {
                if (gl_FragCoord.x <= 1.5 || gl_FragCoord.x >= (res.x - 1.5) || gl_FragCoord.y <= 1.5 || gl_FragCoord.y >= (res.y - 1.5)){
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            }
        }
    </script>
    <script id="Boundary" type="x-shader/x-fragment">
        uniform vec2 res;
        uniform sampler2D read;
        uniform float scale;
        uniform vec2 xloffset;
        uniform vec2 xroffset;
        uniform vec2 yboffset;
        uniform vec2 ytoffset;

        uniform float density;

        void main() {
            if (gl_FragCoord.x <= 1.5){
                gl_FragColor = vec4(scale * texture2D(read, (gl_FragCoord.xy + xloffset) / res.xy).xyz, 1.0);
                if (density > 0.1) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            } else if (gl_FragCoord.x >= (res.x - 1.5)) {
                gl_FragColor = vec4(scale * texture2D(read, (gl_FragCoord.xy + xroffset) / res.xy).xyz, 1.0);
                if (density > 0.1) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            } else if (gl_FragCoord.y <= 1.5) {
                gl_FragColor = vec4(scale * texture2D(read, (gl_FragCoord.xy + yboffset) / res.xy).xyz, 1.0);
                if (density > 0.1) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            } else if (gl_FragCoord.y >= (res.y - 1.5)) {
                gl_FragColor = vec4(scale * texture2D(read, (gl_FragCoord.xy + ytoffset) / res.xy).xyz, 1.0);
                if (density > 0.1) {
                    gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
                }
            } else {
                gl_FragColor = vec4(texture2D(read, gl_FragCoord.xy / res.xy).xyz, 1.0);
            }

        }
            
    </script>
    <script src="js/OrbitControls.js"></script>
    <script src="js/loaders/MTLLoader.js"></script>
    <script src="js/loaders/OBJLoader.js"></script>
    <script src="js/Slab.js"></script>
    <script src="js/Advect.js"></script>
    <script src="js/ExternalVelocity.js"></script>
    <script src="js/ExternalDensity.js"></script>
    <script src="js/ExternalTemperature.js"></script>
    <script src="js/Draw.js"></script>
    <script src="js/Buoyancy.js"></script>
    <script src="js/Jacobi.js"></script>
    <script src="js/Divergence.js"></script>
    <script src="js/SubtractGradient.js"></script>
    <script src="js/VorticityConf.js"></script>
    <script src="js/Curl.js"></script>
    <script src="js/Boundary.js"></script>
    <script src="js/main.js"></script>
    <!-- <script src="js/smoke_main.js"></script> -->
    <a href="https://github.com/hoangcto/cs184-firesimulation" class="github-corner" aria-label="View source on Github"><svg width="80" height="80" viewBox="0 0 250 250" style="fill:#fff; color:#151513; position: absolute; top: 0; border: 0; left: 0; transform: scale(-1, 1);" aria-hidden="true"><path d="M0,0 L115,115 L130,115 L142,142 L250,250 L250,0 Z"></path><path d="M128.3,109.0 C113.8,99.7 119.0,89.6 119.0,89.6 C122.0,82.7 120.5,78.6 120.5,78.6 C119.2,72.0 123.4,76.3 123.4,76.3 C127.3,80.9 125.5,87.3 125.5,87.3 C122.9,97.6 130.6,101.9 134.4,103.2" fill="currentColor" style="transform-origin: 130px 106px;" class="octo-arm"></path><path d="M115.0,115.0 C114.9,115.1 118.7,116.5 119.8,115.4 L133.7,101.6 C136.9,99.2 139.9,98.4 142.2,98.6 C133.8,88.0 127.5,74.4 143.8,58.0 C148.5,53.4 154.0,51.2 159.7,51.0 C160.3,49.4 163.2,43.6 171.4,40.1 C171.4,40.1 176.1,42.5 178.8,56.2 C183.1,58.6 187.2,61.8 190.9,65.4 C194.5,69.0 197.7,73.2 200.1,77.6 C213.8,80.2 216.3,84.9 216.3,84.9 C212.7,93.1 206.9,96.0 205.4,96.6 C205.1,102.4 203.0,107.8 198.3,112.5 C181.9,128.9 168.3,122.5 157.7,114.1 C157.9,116.9 156.7,120.9 152.7,124.9 L141.0,136.5 C139.8,137.7 141.6,141.9 141.8,141.8 Z" fill="currentColor" class="octo-body"></path></svg></a><style>.github-corner:hover .octo-arm{animation:octocat-wave 560ms ease-in-out}@keyframes octocat-wave{0%,100%{transform:rotate(0)}20%,60%{transform:rotate(-25deg)}40%,80%{transform:rotate(10deg)}}@media (max-width:500px){.github-corner:hover .octo-arm{animation:none}.github-corner .octo-arm{animation:octocat-wave 560ms ease-in-out}}</style>
  </body>
</html>